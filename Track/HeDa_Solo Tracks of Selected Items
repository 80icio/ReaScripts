/**
 * ReaScript Name: HeDa_Solo Tracks of Selected Items
 * Description: Automatically solo tracks of selected items
 * Instructions: Run this installation script once. Then use the main script.
 * Author: HeDa
 * Author URl: http://forum.cockos.com/member.php?u=47822
 * Repository: 
 * Repository URl: 
 * File URl: 
 * License: GPL v3
 * Forum Thread: http://forum.cockos.com/showthread.php?t=155988
 * Forum Thread URl: 
 * Version: 0.3 beta
 * REAPER: 4.6
 * Extensions: 
 */
 
/**
 * Change log:
 * v0.3 (2015-03-08)
  # bug fixes
  
 * v0.2 (2015-02-27)
  + REAPER 5 toggle state support (no more window asking to terminate or launch another instance)
  
 * v0.1 (2015-02-21)
  + automatically solo tracks of selected items
  + option to add to selection 
  + REAPER 5 toggle action support.

*/


///////////// OPTIONS //////////



// solo_mode = 1; normal solo
// solo_mode = 2; solo in place
solo_mode = 2;


// time interval. increase to save more CPU
Time_Interval = 0.2;

//////////////////////////////// End of Options






// Don't modify below here 
////////////////////////////////////////////////////////////////////////////////////////////// 


lstate = -1;


function run()
(
  time_precise() - t1 > Time_Interval ? (
  
  PreventUIRefresh(1);
  
  numberitems=CountSelectedMediaItems(0);
  numberitems>0 ? state = 1 : state= 0;
   
  state>0 ? (
    // some items selected or more items selected
    
    // Create array of tracks
    TracksArray=0;
    NumberTracks=0;
    i=0;
    loop(numberitems, 
        (item = GetSelectedMediaItem(0,i)) ? (
          (track = GetMediaItemTrack(item)) ? (
            TracksArray[NumberTracks]=track;
            NumberTracks+=1;
          );        
        );  
        i+=1;
    );  
    //
    
    // solo tracks in array
    Add_to_Selection == 0 ? Main_OnCommand(40340, 0);  // Unsolo all tracks
  
    

    i=0;
    loop(NumberTracks,
      SetMediaTrackInfo_Value(TracksArray[i], "I_SOLO", solo_mode);  // solo tracks
      i+=1;
    );
  
    
  ):( 
    // no items selected
    Main_OnCommand(40340, 0);  // Unsolo all tracks
  );
  

  PreventUIRefresh(-1);
  
  time_precise(t1); // "reset" timer
  );
  
  defer("run();");    // execute mainloop constantly
  
);


//V4>>>
  run();
//V4<<<

